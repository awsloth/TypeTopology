Written 26 March 2023 by Tom de Jong, following a discussion with Nicolai Kraus
on 24 March 2023, but not completed and merged into TypeTopology master then.
Revisited and updated on 17 February 2026.

--- Summary ---
We show that having an identification of the ordinals (ğŸš , â‚€ â‰º â‚) and
(ğŸš , â‚ â‰º â‚€) contradicts the K-axiom. It follows that preunivalence, while
sufficient to show that the type of ordinals is a set
(cf. Ordinal-is-set-under-preunivalence in Ordinals.Equivalence), is not enough
to show that the simulation ordering on the type of ordinals is
antisymmetric. Indeed, the ordinals (ğŸš , â‚€ â‰º â‚) and (ğŸš , â‚ â‰º â‚€) are equivalent,
while preunivalence is derivable from the K-axiom.

--- Proof sketch ---
For any two ordinals Î± and Î², we let Î± â‰ƒâ‚’ Î² denote the type of order
equivalences from Î± to Î². By path induction, the following diagram

                ap âŸ¨_âŸ©
   Î± ï¼ Î² ------------------> âŸ¨ Î± âŸ© ï¼ âŸ¨ Î² âŸ©
     |                              |
     | idtoeq                       | idtoeq
     v                              v
   Î± â‰ƒâ‚’ Î² ------------------> âŸ¨ Î± âŸ© â‰ƒ âŸ¨ Î² âŸ©
              â‰ƒâ‚’-gives-â‰ƒ

always commutes.

Taking Î± = ğŸšâ‚’ = (ğŸš , â‚€ â‰º â‚) and Î² = ğŸšâ‚’' = (ğŸš , â‚ â‰º â‚€), we first observe that
(Î± â‰ƒâ‚’ Î²) has a single inhabitant eâ‚€, namely the order equivalence that swaps the
booleans. Therefore, applying â‰ƒâ‚’-gives-â‰ƒ to *any* element of (Î± â‰ƒâ‚€ Î²) always
yields the swapping equivalence (called complement in TypeTopology).

Assuming the K-axiom, all loops are refl, so that any element p : âŸ¨ Î± âŸ© = âŸ¨ Î² âŸ©
must be equal to refl as âŸ¨ Î± âŸ© and âŸ¨ Î² âŸ© are both just ğŸš. Hence, under the
K-axiom, applying idtoeq to any such element always yields the identity map.

Hence, the commutativity of the diagram then tells us that identifying Î± and Î²
must contradict the K-axiom.

\begin{code}

{-# OPTIONS --safe --without-K #-}

module Ordinals.IdentifyingEquivalentOrdinals where

open import MLTT.Spartan
open import MLTT.Two-Properties
open import UF.Equiv
open import UF.PreUnivalence
open import UF.Sets
open import UF.Subsingletons

open import Ordinals.Equivalence
open import Ordinals.Maps
open import Ordinals.Notions
open import Ordinals.Type
open import Ordinals.Underlying

idtoeqâ‚’-naturality
 : (Î± Î² : Ordinal ğ“¤) (p : Î± ï¼ Î²)
 â†’ idtoeq âŸ¨ Î± âŸ© âŸ¨ Î² âŸ© (ap âŸ¨_âŸ© p) ï¼ â‰ƒâ‚’-gives-â‰ƒ Î± Î² (idtoeqâ‚’ Î± Î² p)
idtoeqâ‚’-naturality Î± Î² refl = refl

\end{code}

We now construct the ordinal ğŸšâ‚€ = (ğŸš , â‚€ â‰º â‚).
Note that it is equivalent to ğŸ™â‚’ +â‚’ ğŸ™â‚’, but we prefer to work directly with booleans here.

\begin{code}

private

 ğŸšâ‚’ : Ordinal ğ“¤â‚€
 ğŸšâ‚’ = ğŸš , (_â‰º_ , p , w , e , t)
  where
   _â‰º_ : ğŸš â†’ ğŸš â†’ ğ“¤â‚€ Ì‡
   â‚€ â‰º â‚€ = ğŸ˜
   â‚€ â‰º â‚ = ğŸ™
   â‚ â‰º â‚€ = ğŸ˜
   â‚ â‰º â‚ = ğŸ˜
   p : is-prop-valued _â‰º_
   p â‚€ â‚€ = ğŸ˜-is-prop
   p â‚€ â‚ = ğŸ™-is-prop
   p â‚ â‚€ = ğŸ˜-is-prop
   p â‚ â‚ = ğŸ˜-is-prop
   w : is-well-founded _â‰º_
   w â‚€ = acc a
    where
     a : (y : ğŸš) â†’ y â‰º â‚€ â†’ is-accessible _â‰º_ y
     a â‚€ l = ğŸ˜-elim l
     a â‚ l = ğŸ˜-elim l
   w â‚ = acc a
    where
     a : (y : ğŸš) â†’ y â‰º â‚ â†’ is-accessible _â‰º_ y
     a â‚€ l = w â‚€
     a â‚ l = ğŸ˜-elim l
   e : is-extensional _â‰º_
   e â‚€ â‚€ u v = refl
   e â‚€ â‚ u v = ğŸ˜-elim (v â‚€ â‹†)
   e â‚ â‚€ u v = ğŸ˜-elim (u â‚€ â‹†)
   e â‚ â‚ u v = refl
   t : is-transitive _â‰º_
   t â‚€ â‚€ â‚€ k l = l
   t â‚€ â‚ â‚€ k l = l
   t â‚ â‚€ â‚€ k l = l
   t â‚ â‚ â‚€ k l = l
   t â‚€ â‚€ â‚ k l = l
   t â‚€ â‚ â‚ k l = k
   t â‚ â‚€ â‚ k l = k
   t â‚ â‚ â‚ k l = l

\end{code}

We now construct the ordinal ğŸšâ‚€' = (ğŸš , â‚ â‰º â‚€).
There is some repetition of code which we could avoid by using
transport-well-order (on the complement map) from Ordinals.WellOrderTransport,
but that module requires function extensionality which we prefer not to assume
here for foundational minimalism.

\begin{code}

 ğŸšâ‚’' : Ordinal ğ“¤â‚€
 ğŸšâ‚’' = ğŸš , (_â‰º_ , p , w , e , t)
  where
   _â‰º_ : ğŸš â†’ ğŸš â†’ ğ“¤â‚€ Ì‡
   â‚€ â‰º â‚€ = ğŸ˜
   â‚€ â‰º â‚ = ğŸ˜
   â‚ â‰º â‚€ = ğŸ™
   â‚ â‰º â‚ = ğŸ˜
   p : is-prop-valued _â‰º_
   p â‚€ â‚€ = ğŸ˜-is-prop
   p â‚€ â‚ = ğŸ˜-is-prop
   p â‚ â‚€ = ğŸ™-is-prop
   p â‚ â‚ = ğŸ˜-is-prop
   w : is-well-founded _â‰º_
   w â‚€ = acc a
    where
     a : (y : ğŸš) â†’ y â‰º â‚€ â†’ is-accessible _â‰º_ y
     a â‚€ l = ğŸ˜-elim l
     a â‚ l = w â‚
   w â‚ = acc a
    where
     a : (y : ğŸš) â†’ y â‰º â‚ â†’ is-accessible _â‰º_ y
     a â‚€ l = ğŸ˜-elim l
     a â‚ l = ğŸ˜-elim l
   e : is-extensional _â‰º_
   e â‚€ â‚€ u v = refl
   e â‚€ â‚ u v = ğŸ˜-elim (u â‚ â‹†)
   e â‚ â‚€ u v = ğŸ˜-elim (v â‚ â‹†)
   e â‚ â‚ u v = refl
   t : is-transitive _â‰º_
   t â‚€ â‚€ â‚€ k l = l
   t â‚€ â‚ â‚€ k l = k
   t â‚ â‚€ â‚€ k l = â‹†
   t â‚ â‚ â‚€ k l = l
   t â‚€ â‚€ â‚ k l = l
   t â‚€ â‚ â‚ k l = l
   t â‚ â‚€ â‚ k l = l
   t â‚ â‚ â‚ k l = l

\end{code}

The two ordinals are equivalent via the complement map and of course this is the
only order equivalence between the two ordinals.

\begin{code}

 ğŸšâ‚’-â‰ƒâ‚’-ğŸšâ‚’' : ğŸšâ‚’ â‰ƒâ‚’ ğŸšâ‚’'
 ğŸšâ‚’-â‰ƒâ‚’-ğŸšâ‚’' = f , f-preserves-order , f-is-equiv , f-preserves-order'
  where
   f : ğŸš â†’ ğŸš
   f = complement
   f-preserves-order : is-order-preserving ğŸšâ‚’ ğŸšâ‚’' f
   f-preserves-order â‚€ â‚ l = l
   f-preserves-order â‚€ â‚€ l = ğŸ˜-elim l
   f-preserves-order â‚ â‚€ l = ğŸ˜-elim l
   f-preserves-order â‚ â‚ l = ğŸ˜-elim l
   f-is-equiv : is-equiv f
   f-is-equiv = qinvs-are-equivs f (f , complement-involutive , complement-involutive)
   f-preserves-order' : is-order-preserving ğŸšâ‚’' ğŸšâ‚’ f
   f-preserves-order' â‚€ â‚€ l = ğŸ˜-elim l
   f-preserves-order' â‚€ â‚ l = ğŸ˜-elim l
   f-preserves-order' â‚ â‚€ l = l
   f-preserves-order' â‚ â‚ l = ğŸ˜-elim l

 complement-is-the-only-ordinal-equivalence-of-ğŸš
  : (e : ğŸšâ‚’ â‰ƒâ‚’ ğŸšâ‚’') â†’ â‰ƒâ‚’-to-fun ğŸšâ‚’ ğŸšâ‚’' e âˆ¼ complement
 complement-is-the-only-ordinal-equivalence-of-ğŸš e â‚€ = II
   where
    f : ğŸš â†’ ğŸš
    f = â‰ƒâ‚’-to-fun ğŸšâ‚’ ğŸšâ‚’' e
    I : â‰ƒâ‚’-to-fun ğŸšâ‚’ ğŸšâ‚’' e â‚€ â‰  â‚€
    I p = l' (f â‚) (order-equivs-are-order-preserving ğŸšâ‚’ ğŸšâ‚’'
                     (â‰ƒâ‚’-to-fun-is-order-equiv ğŸšâ‚’ ğŸšâ‚’' e)
                     â‚€ â‚ â‹†)
     where
      l : (b : ğŸš) â†’ Â¬ (â‚€ â‰ºâŸ¨ ğŸšâ‚’' âŸ© b)
      l â‚€ l = ğŸ˜-elim l
      l â‚ l = ğŸ˜-elim l
      l' : (b : ğŸš) â†’ Â¬ (f â‚€ â‰ºâŸ¨ ğŸšâ‚’' âŸ© b)
      l' b = idtofun _ _ (ap (Î» - â†’ Â¬ (- â‰ºâŸ¨ ğŸšâ‚’' âŸ© b)) (p â»Â¹)) (l b)
    II : f â‚€ ï¼ â‚
    II = different-from-â‚€-equal-â‚ I
 complement-is-the-only-ordinal-equivalence-of-ğŸš e â‚ = II
  where
   f : ğŸš â†’ ğŸš
   f = â‰ƒâ‚’-to-fun ğŸšâ‚’ ğŸšâ‚’' e
   I : â‰ƒâ‚’-to-fun ğŸšâ‚’ ğŸšâ‚’' e â‚ â‰  â‚
   I p = l' (f â‚€) (order-equivs-are-order-preserving ğŸšâ‚’ ğŸšâ‚’'
                    (â‰ƒâ‚’-to-fun-is-order-equiv ğŸšâ‚’ ğŸšâ‚’' e)
                    â‚€ â‚ â‹†)
    where
     l : (b : ğŸš) â†’ Â¬ (b â‰ºâŸ¨ ğŸšâ‚’' âŸ© â‚)
     l â‚€ l = ğŸ˜-elim l
     l â‚ l = ğŸ˜-elim l
     l' : (b : ğŸš) â†’ Â¬ (b â‰ºâŸ¨ ğŸšâ‚’' âŸ© f â‚)
     l' b = idtofun _ _ (ap (Î» - â†’ Â¬ (b â‰ºâŸ¨ ğŸšâ‚’' âŸ© -)) (p â»Â¹)) (l b)
   II : f â‚ ï¼ â‚€
   II = different-from-â‚-equal-â‚€ I

\end{code}

As announced, identifying the ordinals ğŸšâ‚€ and ğŸšâ‚’' contradicts the K-axiom.

\begin{code}

 identification-of-ğŸšâ‚’-and-ğŸšâ‚’'-contradicts-K : ğŸšâ‚’ ï¼ ğŸšâ‚’' â†’ Â¬ K-axiom ğ“¤â‚
 identification-of-ğŸšâ‚’-and-ğŸšâ‚’'-contradicts-K pâ‚’ K =
  p-is-not-refl (K (ğ“¤â‚€ Ì‡  ) p refl)
   where
    p : ğŸš ï¼ ğŸš
    p = ap âŸ¨_âŸ© pâ‚’

    f : ğŸš â†’ ğŸš
    f = âŒœ idtoeq ğŸš ğŸš p âŒ
    f' : ğŸš â†’ ğŸš
    f' = âŒœ â‰ƒâ‚’-gives-â‰ƒ ğŸšâ‚’ ğŸšâ‚’' (idtoeqâ‚’ ğŸšâ‚’ ğŸšâ‚’' pâ‚’) âŒ

    I : f âˆ¼ f'
    I b = ap (Î» - â†’ âŒœ - âŒ b) (idtoeqâ‚’-naturality ğŸšâ‚’ ğŸšâ‚’' pâ‚’)

    II : f' âˆ¼ complement
    II = complement-is-the-only-ordinal-equivalence-of-ğŸš (idtoeqâ‚’ ğŸšâ‚’ ğŸšâ‚’' pâ‚’)

    p-is-not-refl : Â¬ (p ï¼ refl)
    p-is-not-refl e = zero-is-not-one
     (â‚€                     ï¼âŸ¨ refl âŸ©
      âŒœ idtoeq ğŸš ğŸš refl âŒ â‚€ ï¼âŸ¨ ap (Î» - â†’ âŒœ idtoeq ğŸš ğŸš - âŒ â‚€) (e â»Â¹) âŸ©
      f â‚€                   ï¼âŸ¨ I â‚€ âŸ©
      f' â‚€                  ï¼âŸ¨ II â‚€ âŸ©
      â‚                     âˆ)

\end{code}

In particular, antisymmetry of the simulation preorder contradicts the K-axiom.
Note that we copied the definition of the simulation preorder âŠ´ here from
Ordinals.OrdinalOfOrdinals since that module assumes univalence which we wish to
avoid here.

\begin{code}

 _âŠ´_ : Ordinal ğ“¤ â†’ Ordinal ğ“¥ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
 Î± âŠ´ Î² = Î£ f ê‰ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©) , is-simulation Î± Î² f

 antisymmetry-of-âŠ´ : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
 antisymmetry-of-âŠ´ ğ“¤ = (Î± Î² : Ordinal ğ“¤) â†’ Î± âŠ´ Î² â†’ Î² âŠ´ Î± â†’ Î± ï¼ Î²

antisymmetry-of-âŠ´-contradicts-K : antisymmetry-of-âŠ´ ğ“¤â‚€ â†’ Â¬ K-axiom ğ“¤â‚
antisymmetry-of-âŠ´-contradicts-K âŠ´-antisym =
 identification-of-ğŸšâ‚’-and-ğŸšâ‚’'-contradicts-K I
  where
   I : ğŸšâ‚’ ï¼ ğŸšâ‚’'
   I = âŠ´-antisym ğŸšâ‚’ ğŸšâ‚’' Iâ‚ Iâ‚‚
    where
     Iâ‚ : ğŸšâ‚’ âŠ´ ğŸšâ‚’'
     Iâ‚ = (complement ,
            order-equivs-are-simulations ğŸšâ‚’ ğŸšâ‚’' complement (prâ‚‚ ğŸšâ‚’-â‰ƒâ‚’-ğŸšâ‚’'))
     Iâ‚‚ : ğŸšâ‚’' âŠ´ ğŸšâ‚’
     Iâ‚‚ = (complement , order-equivs-are-simulations ğŸšâ‚’' ğŸšâ‚’ complement
                          (prâ‚‚ (â‰ƒâ‚’-sym ğŸšâ‚’ ğŸšâ‚’' (ğŸšâ‚’-â‰ƒâ‚’-ğŸšâ‚’'))))
\end{code}

Finally, we show that preunivalence cannot prove that âŠ´ is antisymmetric.
The argument uses that the K-axiom is not false (in the absence of univalence),
but of course it is not provable either, so we add Â¬Â¬ K-axiom as a hypothesis.

We also need a small technical lemma, K-axiom-lower, that is perhaps better
placed in UF.Sets-Properties which it can't right now because of cyclic module
dependencies (which we could avoid by replacing â‰ƒ-Lift with an inline
construction).

\begin{code}

K-axiom-lower : K-axiom (ğ“¤ âº) â†’ K-axiom ğ“¤
K-axiom-lower {ğ“¤} K X = I
 where
  open import UF.Sets-Properties
  open import UF.UniverseEmbedding
  I : is-set X
  I = equiv-to-set (â‰ƒ-Lift (ğ“¤ âº) X) (K (Lift (ğ“¤ âº) X))

preunivalence-cannot-show-antisymmetry-of-âŠ´
 : Â¬Â¬ K-axiom ğ“¤â‚
 â†’ Â¬ (is-preunivalent ğ“¤â‚€ â†’ antisymmetry-of-âŠ´ ğ“¤â‚€)
preunivalence-cannot-show-antisymmetry-of-âŠ´ K-consistent hyp = V
 where
  I : K-axiom ğ“¤â‚ â†’ is-preunivalent ğ“¤â‚€
  I K = K-gives-preunivalence (K-axiom-lower K) K

  II : is-preunivalent ğ“¤â‚€ â†’ antisymmetry-of-âŠ´ ğ“¤â‚€
  II = hyp

  III : antisymmetry-of-âŠ´ ğ“¤â‚€ â†’ Â¬ K-axiom ğ“¤â‚
  III = antisymmetry-of-âŠ´-contradicts-K

  IV : K-axiom ğ“¤â‚ â†’ Â¬ K-axiom ğ“¤â‚
  IV = III âˆ˜ II âˆ˜ I

  V : ğŸ˜
  V = K-consistent (Î» K â†’ IV K K)

\end{code}